@Declear -> return | if | else | while | void | int | <ID> | <INT> | ; | , | ( | ) | { | } | + | - | * | / | = | > | < | >= | <= | != | ==

S -> Program
Program -> ExtDefList 

ExtDefList -> ExtDef ExtDefList | @
ExtDef -> VarSpecifier <ID> ; | FunSpecifier FunDec Block
VarSpecifier -> int
FunSpecifier -> void | int 
FunDec -> <ID> CreateFunTable_m ( VarList )
CreateFunTable_m -> @
VarList -> ParamDec , VarList | ParamDec | @
ParamDec -> VarSpecifier <ID>
Block -> { DefList StmtList }
DefList -> Def DefList | @
Def -> VarSpecifier <ID> ;
StmtList -> Stmt StmtList | @
Stmt -> AssignStmt ; | ReturnStmt ; | IfStmt | WhileStmt | CallStmt ;
AssignStmt -> <ID> = Exp
Exp -> AddSubExp | Exp Relop AddSubExp
AddSubExp -> Item | Item + Item | Item - Item
Item -> Factor | Factor * Factor | Factor / Factor
Factor -> <INT> | ( Exp ) | <ID> | CallStmt
CallStmt -> <ID> ( CallFunCheck Args )
CallFunCheck -> @
Args -> Exp , Args | Exp | @
ReturnStmt -> return Exp | return
Relop -> > | < | >= | <= | == | !=
IfStmt -> if IfStmt_m1 ( Exp ) IfStmt_m2 Block IfNext
IfStmt_m1 -> @
IfStmt_m2 -> @
IfNext -> @ | IfStmt_next else Block
IfStmt_next -> @
WhileStmt -> while WhileStmt_m1 ( Exp ) WhileStmt_m2 Block
WhileStmt_m1 -> @
WhileStmt_m2 -> @